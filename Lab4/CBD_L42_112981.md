# NMEC: 112981

## Lab04 - Ex 4.2

### 1. Encontre todos os atores que dirigiram um filme em que também atuaram e apresente o nome do ator e o título do filme.
```sql
MATCH (a:Person)-[:DIRECTED]->(m:Movie)<-[:ACTED_IN]-(a) RETURN a.name AS Actor, m.title AS Movie
```

### 2. Para cada filme realizado depois de 2005, apresente os nomes de todos os atores que atuaram nesse filme.
```sql
MATCH (a:Person)-[:ACTED_IN]->(m:Movie) WHERE m.released > 2005 RETURN m.title AS Movie, COLLECT(a.name) AS Actors
```

### 3. Encontre pares de nós com mais do que uma relação entre si.
```sql
MATCH (a)-[r]-(b) WITH a, b, count(r) AS rels WHERE rels > 1 RETURN a, b, rels
```

### 4. Encontre todos os pares de pessoas que fizeram revisões do mesmo filme. Apresente os seus nomes e título de cada filme.
```sql
MATCH (p1:Person)-[:REVIEWED]->(m:Movie)<-[:REVIEWED]-(p2:Person) WHERE p1 <> p2 AND p1.name < p2.name RETURN p1.name AS Person1, p2.name AS Person2, m.title AS Movie
```

### 5. Encontre todos os pares de atores que atuaram em vários filmes juntos.
```sql
MATCH (a1:Person)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(a2:Person) WHERE a1 <> a2 AND a1.name < a2.name WITH a1, a2, count(m) AS movies WHERE movies > 1 RETURN a1.name AS Actor1, a2.name AS Actor2, movies AS Movies
```

### 6. Determine a idade média do elenco do filme "Apollo 13" no ano do lançamento do filme.
```sql
MATCH (m:Movie {title: "Apollo 13"})<-[:ACTED_IN]-(a:Person) RETURN m.title AS Movie, m.released AS Released, avg(m.released - a.born) AS AverageAge
```

### 7. Encontre os 10 filmes com o elenco mais velho no momento do lançamento do filme. Apresente o filme e a idade média arredondada a 2 casas decimais, por ordem decrescente.
```sql
MATCH (m:Movie)<-[:ACTED_IN]-(a:Person) WITH m, (m.released - a.born) AS age RETURN m.title AS Movie, round(avg(age), 2) AS AverageAge ORDER BY AverageAge DESC LIMIT 10
```

### 8. Apresente o subgrafo ACTED_IN do filme com o elenco mais novo, no momento do lançamento do filme.
```sql
MATCH (m:Movie)<-[:ACTED_IN]-(a:Person) WITH m, avg((m.released - a.born)) AS age, COLLECT(a) AS actors RETURN m, actors ORDER BY age ASC LIMIT 1 
```

### 9. Qual é o caminho mais curto (usando qualquer tipo de relação) entre John Cusack e Demi Moore?
```sql
MATCH p=shortestPath((a:Person {name: "John Cusack"})-[*]-(b:Person {name: "Demi Moore"})) RETURN p -- as i've done it in the neo4j demo
```

### 10. Qual a dimensão caminho mais curto (usando qualquer tipo de relação) entre Keanu Reeves e Tom Cruise?
```sql
MATCH p=shortestPath((a:Person {name: "Keanu Reeves"})-[*]-(b:Person {name: "Tom Cruise"})) RETURN length(p)
```

### 11. Quais são as dimensões do caminho mais curto entre pessoas com nome Jim e pessoas com nome Kevin?
```sql
MATCH (a:Person) WHERE a.name STARTS WITH "Jim" MATCH (b:Person) WHERE b.name STARTS WITH "Kevin" MATCH p=shortestPath((a)-[*]-(b)) RETURN length(p)
```

Note: See the different Jim's --> MATCH (a:Person) WHERE a.name STARTS WITH "Jim" RETURN COLLECT(a.name) as Jim

### 12. Que pessoas têm uma distância 2 para Jim Cash (a distância entre duas pessoas é o comprimento do caminho mais curto entre eles)?
```sql
MATCH (a:Person {name: "Jim Cash"})-[*2]-(b:Person) RETURN b.name
```

<!-- ### Relaçẽes entre Jim Cash e Kelly McGillis
```sql
MATCH p=(a:Person {name: "Jim Cash"})-[*1..5]-(b:Person {name: "Kelly McGillis"}) RETURN p
``` -->

### 13. Qual é a maior distância de uma pessoa para Kevin Bacon?
```sql
MATCH p=shortestPath((a:Person {name: "Kevin Bacon"})-[*1..7]-(b:Person)) WHERE a <> b RETURN max(length(p)) 
```

### 14. Qual é a maior distância entre duas pessoas?
```sql
MATCH p=shortestPath((a:Person)-[*1..12]-(b:Person)) WHERE a <> b RETURN max(length(p))
```

### 15. Qual é a distribuição de distâncias em pares (isto é, para a distância 1, 2, 3, ..., quantos pares de pessoas têm essa distância um do outro)?
```sql
MATCH p=shortestPath((a)-[*1..12]-(b)) WHERE a <> b AND a.name < b.name RETURN length(p) AS Distance, count(*) AS Pairs ORDER BY Distance
```

### 16. Indique as 10 pessoas com menor distância média em que o caminho entre elas são relações do tipo ACTED_IN.
```sql
MATCH p=shortestPath((a)-[:ACTED_IN*]-(b)) WHERE a <> b AND a.name < b.name  RETURN a.name AS Person1, round(avg(length(p)), 2) AS AverageDistance ORDER BY AverageDistance LIMIT 10
```

### 17. Apresente os atores que tiveram pelo menos uma role para personagens com o prefixo 'Dr.'. O resultado deve ser uma lista com o par “Ator, Lista das roles”.
```sql
MATCH (actor:Person)-[r:ACTED_IN]->(movie:Movie)
UNWIND r.roles AS role
WITH actor, role
WHERE role STARTS WITH "Dr."
WITH actor.name AS Actor, collect(role) AS Roles
RETURN [Actor, Roles] AS ActorRoles
```

### 18. Encontre todos os diretores que trabalharam em filmes com mais de 7 atores e apresente o nome do diretor e o título do filme.
```sql
MATCH (d:Person)-[:DIRECTED]->(m:Movie)<-[:ACTED_IN]-(a:Person) WITH d, m, count(a) AS actors WHERE actors > 7 RETURN d.name AS Director, m.title AS Movie
```

### 19. Encontre pares de atores que atuaram em mais de um filme juntos e apresente o nome dos atores e o número de filmes em que atuaram juntos.
```sql
MATCH (a1:Person)-[:ACTED_IN]->(m:Movie)<-[:ACTED_IN]-(a2:Person)
WHERE a1 <> a2 AND a1.name < a2.name
WITH a1.name AS actor1, a2.name AS actor2, COUNT(m) AS sameMovies
WHERE sameMovies > 1
RETURN actor1, actor2, sameMovies
ORDER BY sameMovies DESC
```

### 20. Encontre os 10 atores que mais trabalharam com o mesmo diretor e apresente o nome do ator, o nome do diretor e o número de filmes em que trabalharam juntos.
```sql
MATCH (a:Person)-[:ACTED_IN]->(m:Movie)<-[:DIRECTED]-(d:Person)
WITH a, d, COUNT(m) AS n_movies
ORDER BY n_movies DESC
RETURN a.name AS Actor, d.name AS Director, n_movies
LIMIT 10
```

