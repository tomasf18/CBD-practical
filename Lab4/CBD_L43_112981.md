# NMEC: 112981

# Lab04 - Ex 4.3

## 0. Carregue o conteudo do ficheiro `/resouces/git_selection.csv`.

```sql
LOAD CSV WITH HEADERS FROM 'file:///resources/git_selection.csv' AS row
WITH 
    trim(row.svn_id) AS svn_id,
    trim(row.real_name) AS real_name,
    trim(row.project_name) AS project_name,
    trim(row.role_on_project) AS role_on_project,
    trim(row.organization) AS organization,
    row.num AS num
WITH DISTINCT 
    toLower(replace(svn_id, ' ', '')) AS svn_id, 
    toLower(replace(real_name, ' ', '')) AS real_name, 
    toLower(replace(project_name, ' ', '')) AS project_name, 
    toLower(replace(role_on_project, ' ', '')) AS role_on_project, 
    toLower(replace(organization, ' ', '')) AS organization

MERGE (person:Person {svn_id: svn_id}) 
ON CREATE SET person.real_name = real_name

MERGE (org:Organization {name: organization})

MERGE (project:Project {name: project_name, organization: organization})

MERGE (person)-[:WORKS_ON]->(org)

MERGE (person)-[r:CONTRIBUTES]->(project)
ON CREATE SET r.roles = [role_on_project]
ON MATCH SET r.roles = CASE WHEN role_on_project IN r.roles THEN r.roles ELSE r.roles + role_on_project END
```

### Explicação do Load:

1. Carregar o conteúdo do ficheiro `git_selection.csv` para a base de dados Neo4J.
2. Remover os dados duplicados e/ou com erros:
    - Normalizar os dados, removendo espaços com as funções `trim` e `replace`.
    - Converter o resultado de 2. para minúsculas.
3. Criar os nós `Person`, `Organization` e `Project` e as relações `WORKS_ON` e `CONTRIBUTES` entre os nós.
    - Se o nó `Person` não existir, criar um novo nó com o `svn_id` e `real_name`.
    - Se o nó `Organization` não existir, criar um novo nó com o `name`.
    - Se o nó `Project` não existir, criar um novo nó com o `name`.
    - Criar a relação `WORKS_ON` entre o nó `Person` e o nó `Organization`.
    - Criar a relação `CONTRIBUTES` entre o nó `Person` e o nó `Project`.
        - Se a relação já existir, adicionar a `role_on_project` à lista de `roles`.

**Nota**: Decidi converter tudo para minúsculas porque concluí que seria o melhor método para perder menos informação. Pensei em colocar para CamelCase, mas vários casos como o seguinte não seriam tratados:
 - Existem linhas com: `Red Hat` | `RedHat` | `Redhat` -> O primeiro e o segundo tratavam-se, visto que ficavam iguais e eram fundidos, mas o terceiro não.


## 1. Liste a informação de cada utilizador.
```sql
MATCH (per:Person) RETURN per.svn_id AS svn_id, per.real_name AS real_name
```
    
## 2. Liste o nome de cada utilizador.
```sql
MATCH (per:Person) RETURN per.real_name AS real_name
```

## 3. Liste a informação de cada projeto, no qual tenha participado pelo menos um utilizador.
```sql
MATCH (p:Project)<-[:CONTRIBUTES]-(per:Person) RETURN p.name AS project_name, p.organization AS organization, collect(per.real_name) AS contributors
```

## 4. Liste os utilizadores e total de projetos em que cada um colabora.
```sql
MATCH (per:Person)-[:CONTRIBUTES]->(p:Project) RETURN per.svn_id AS svn_id, per.real_name AS real_name, count(p) AS total_projects
```

## 5. Liste os utilizadores e do total de projetos em que cada colabora ordenados por ordem decrescente do total.
```sql
MATCH (per:Person)-[:CONTRIBUTES]->(p:Project) RETURN per.svn_id AS svn_id, per.real_name AS real_name, count(p) AS total_projects ORDER BY total_projects DESC
```

## 6. Liste projetos e total de membros em cada projeto.
```sql
MATCH (p:Project)<-[:CONTRIBUTES]-(per:Person) RETURN p.name AS project_name, count(per) AS total_members
```

## 7. Liste projetos e total de membros com a role "Committer" em cada projeto.
```sql
MATCH (p:Project)<-[:CONTRIBUTES {roles: ['committer']}]-(per:Person) RETURN p.name AS project_name, count(per) AS total_committers
```
**Nota:** Como passei tudo para minúsculas, a role "Committer" foi alterada para "committer".

## 8. Liste todos os utilizadores que participaram nos mesmo projetos do que o utilizador "atm" (id). Mostre os atributos: nome de "atm", nome utilizador 2, nome do projeto.
```sql
MATCH (u1:Person {svn_id: 'atm'})-[:CONTRIBUTES]->(p:Project)<-[:CONTRIBUTES]-(u2:Person) RETURN u1.real_name AS atm, u2.real_name AS user2, p.name AS project_name
```

## 9. Liste todos os utilizadores que participaram com a role "Committer" em projetos em que o utilizador "atm" (id1) participou com a role "PMC".
```sql
MATCH (u1:Person {svn_id: 'atm'})-[r1:CONTRIBUTES]->(p:Project)<-[r2:CONTRIBUTES]-(u2:Person) 
WHERE 'pmc' IN r1.roles AND 'committer' IN r2.roles
RETURN u1.real_name AS atm, u2.real_name AS user2, p.name AS project_name
```
**Nota:** Como passei tudo para minúsculas, a role "PMC" foi alterada para "pmc" e a role "Committer" foi alterada para "committer".

